class Game {
    field int key, score;
    field boolean running;
    field Snake snake;
    field Food food;
    field Random rng;

    constructor Game new(int rnKey) {
        let key = 0;
        let score = -1;  // setFood updates score;
        let running = true;
        let snake = Snake.new();
        let rng = Random.new(rnKey * rnKey);
        let food = Food.new(8, 8);
        do setFood();

        return this;
    }

    method void dispose() {
        do snake.dispose();
        do rng.dispose();
        do Memory.deAlloc(this);

        return;
    }

    method void tick() {
        var int direction, hit;

        let key = Keyboard.keyPressed();

        if (key = 131) { do snake.setDirection(1); }
        if (key = 132) { do snake.setDirection(2); }
        if (key = 133) { do snake.setDirection(3); }
        if (key = 130) { do snake.setDirection(4); }

        do snake.tick();

        // 0 - no collision, 1 - self or wall, 2 - food
        let hit = snake.checkCollision(food);
        if (hit = 0) { return; }
        if (hit = 1) { let running = false; return; }
        if (hit = 2) { do snake.grow(); do setFood(); return; }

        return;
    }

    // Ideally would go in a helper library or we
    // could expand Math to have this, but eh, I 
    // just need it once to help set the food location
    function int mod(int n, int m) {
        let n = Math.abs(n);
        while(n > m) {
            let n = n - m;
        }

        return n;
    }

    method void setFood() {
        var int x, y, tmp;

        // Generate random coords, and do a poor mans mod
        // to keep them in bounds
        let tmp = rng.next();
        let x = Game.mod(tmp, 64);

        let tmp = rng.next();
        let y = Game.mod(tmp, 32);

        do food.set(x, y);
        let score = score + 1;

        return;
    }

    method void renderScore() {
        do Output.moveCursor(0, 23);
        do Output.printString("Score: ");
        do Output.printInt(score);
        return;
    }


    method void render() {
        do Screen.clearScreen();
        do snake.render();
        do food.render();
        do renderScore();
        return;
    }

    method void gameOver() {
        do Output.moveCursor(10, 15);
        do Output.printString("Game Over Man, Game Over!");
        return;
    }

    method void start() {
        while(running) {
            do tick();
            // Either Render next frame or exit game
            if (~running) { return; }
            do render();
            do Sys.wait(100);
        }

        return;
    }
}
